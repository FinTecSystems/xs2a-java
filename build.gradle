plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation('com.squareup.moshi:moshi-kotlin:1.12.0')
    implementation('com.squareup.moshi:moshi-adapters:1.12.0')
    implementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.5.21'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

group = 'com.fintecsystems'
archivesBaseName = 'xs2a-java'
version = '1.0.0'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        xs2aJava(MavenPublication) {
            groupId = group
            artifactId = 'xs2a-java'
            version = version

            from components.java

            pom {
                name = 'XS2A Java'
                description = 'Java SDK for XS2A API'
                url = 'https://github.com/FinTecSystems/xs2a-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'maik-mursall'
                        name = 'Maik Mursall'
                        email = 'mmursall@fintecsystems.com'
                    }
                    developer {
                        id = 'fritzfr'
                        name = 'Felix Fritz'
                        email = 'ffritz@fintecsystems.com'
                    }
                }
                scm {
                    connection = 'https://github.com/FinTecSystems/xs2a-java.git'
                    developerConnection = 'https://github.com/FinTecSystems/xs2a-java/issues'
                    url = 'https://github.com/FinTecSystems/xs2a-java'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.property('ossrhUsername')
                password = project.property('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

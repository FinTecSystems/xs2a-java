plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.dokka' version "1.5.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.21'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation('com.squareup.moshi:moshi-kotlin:1.12.0')
    implementation('com.squareup.moshi:moshi-adapters:1.12.0')
    implementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test:1.5.21'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        xs2aJava(MavenPublication) {
            groupId = GROUP
            artifactId = POM_ARTIFACT_ID
            version = VERSION_NAME

            from components.java

            pom {
                name = POM_NAME
                description = POM_DESCRIPTION
                url = POM_URL
                packaging = POM_PACKAGING

                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_LICENCE_URL
                        distribution = POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id = 'maik-mursall'
                        name = 'Maik Mursall'
                        email = 'mmursall@fintecsystems.com'
                    }
                    developer {
                        id = 'fritzfr'
                        name = 'Felix Fritz'
                        email = 'ffritz@fintecsystems.com'
                    }
                }

                organization {
                    name = POM_ORGANIZATION_NAME
                    url = POM_ORGANIZATION_URL
                }

                scm {
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                    url = POM_SCM_URL
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.property('ossrhUsername')
                password = project.property('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task generateDocs(dependsOn: dokkaHtml) {
    doLast {
        delete "${rootDir}/docs"

        file("${buildDir}/dokka/html")
            .renameTo(file("${rootDir}/docs"))
    }
}
